\chapter{Perception Module}
\label{chap:observer}

%% Outline
%% - Relationships
%% - Observations
%% - Full Analysis Procedure
%% - Auxillary Segments
%% - Discussion

\section{Overview}

The perception module focuses on ``seeing'' figures and simulating our
mind's eye.  Given analytic figures represented using structures of
the imperative construction module, the perception module is concerned
with finding and reporting interesting relationships seen in the
figure. In a generate-and-test-like fashion, it is rather liberal in
the observations it returns. The module uses several techniques to
attempt to omit obvious properties, and combines with the learning
module (Chapter~\ref{chap:learning}) to filter already-learned
discoveries and simplify results.

To explain the module, I will first describe the implementation of
underlying relationship and observation structures before examining
the full analyzer routine. I will conclude with a discussion of
extensions to the module, including further ways to detect and remove
obvious results and some attempted techniques used to extract
auxiliary relationships from figures.

\section{Relationships}

Relationships are the primary structures defining what constitutes
interesting properties in a figure. Relationships are represented as
predicates over typed n-tuples and are checked against all such
n-tuples found in the figure under analysis.

\begin{code-listing}
[label=relationship-structure]
{Relationships}
(define-record-type <relationship>
  (make-relationship type arity predicate equivalence-predicate)
  relationship?
  (type relationship-type)
  (arity relationship-arity)
  (predicate relationship-predicate)
  (equivalence-predicate relationship-equivalence-predicate))

(define equal-length-relationship
  (%make-relationship 'equal-length 2 segment-equal-length?
                      (set-equivalent-procedure segment-equivalent?)))

(define concurrent-relationship
  (make-relationship 'concurrent 3 concurrent?
                      (set-equivalent-procedure linear-element-equivalent?)))

(define concentric-relationship
  (make-relationship 'concentric 4 concentric?
                      (set-equivalent-procedure point-equal?)))
\end{code-listing}

Listing~\ref{relationship-structure} displays some representative
relationships.  The relationship predicates can be arbitrary Scheme
procedures and often use constructions and utilities from the
underlying imperative system as seen in
Listing~\ref{concurrent-concentric}.  \texttt{concurrent?} is checked
over all 3-tuples of linear elements (lines, rays, segments) and
\texttt{concentric?} is checked against all 4-tuples of points.

\begin{code-listing}
[label=concurrent-concentric]
{Concurrent and Concentric Predicates}
(define (concurrent? l1 l2 l3)
  (let ((i-point (intersect-linear-elements-no-endpoints l1 l2)))
    (and i-point
         (on-element? i-point l3)
         (not (element-endpoint? i-point l3)))))

(define (concentric? p1 p2 p3 p4)
  (and (distinct? p1 p2 p3 p4)
       (let ((pb-1 (perpendicular-bisector (make-segment p1 p2)))
             (pb-2 (perpendicular-bisector (make-segment p2 p3)))
             (pb-3 (perpendicular-bisector (make-segment p3 p4))))
         (concurrent? pb-1 pb-2 pb-3))))
\end{code-listing}

In addition to the type, arity, and predicate checked against
arguments, the relationship structure also includes an equivalence
predicate that is used in determining whether two observations using
the relationship are equivalent, as will be discussed after explaining
the observation structure in Section~\ref{sec:obs}.

\subsection{What is Interesting?}
The system currently checks for:
\begin{spacing}{0.7}
\begin{itemize}
\item concurrent, parallel, and perpendicular linear elements,
\item segments of equal length,
\item supplementary and complementary angles,
\item angles of equal measure,
\item coincident and concentric points, and
\item sets of three concentric points with a fourth as its center.
\end{itemize}
\end{spacing}
\noindent These relationships covered most of the basic observations needed in
my investigations, but further relationships can be easily added.

\section{Observations}
\label{sec:obs}

Observations are structures used to report the analyzer's findings. As
seen in Listing~\ref{obs}, they combine the relevant relationship
structure with a list of the actual element arguments from the figure
that satisfy that relationship. Maintaining references to the actual
figure elements allows helper procedures to print names or extract
dependencies as needed.

\begin{code-listing}
[label=obs]
{Observations}
(define-record-type <observation>
  (make-observation relationship args)
  observation?
  (relationship observation-relationship)
  (args observation-args))
\end{code-listing}

It is important to know whether two arbitrary observations are
equivalent. This enables the system to detect and avoid reporting
redundant or uninteresting relationships.  Listing~\ref{eqv-obs} shows
the implementation of \texttt{observation-equivalent?}. The procedure
checks the observations are the same and then applies that
observation's equivalence predicate to the two tuples of observation
arguments.

\begin{code-listing}
[label=eqv-obs]
{Equivalent Observations}
(define (observation-equivalent? obs1 obs2)
  (and (relationship-equal?
        (observation-relationship obs1)
        (observation-relationship obs2))
       (let ((rel-eqv-test
              (relationship-equivalence-predicate
               (observation-relationship obs1)))
             (args1 (observation-args obs1))
             (args2 (observation-args obs2)))
         (rel-eqv-test args1 args2))))
\end{code-listing}

These equivalence predicates handle the various patterns in which
objects may appear in observations. For example, in an observation
that two segments have equal length, it does not matter which segment
comes first or which order the endpoints are listed within each
segment. Thus, as shown in Listing~\ref{segment-equal-eqv}, the
equivalence procedure ignores these ordering differences by comparing
set equalities:

\begin{code-listing}
[label=segment-equal-eqv]
{Equivalence of Equal Segment Length Observations}
(set-equivalent-procedure segment-equivalent?)

(define (set-equivalent-procedure equality-predicate)
  (lambda (set1 set2)
    (set-equivalent? set1 set2 equality-predicate)))

(define (set-equivalent? set1 set2 equality-predicate)
  (and (subset? set1 set2 equality-predicate)
       (subset? set2 set1 equality-predicate)))

(define (segment-equivalent? s1 s2)
  (set-equivalent?
   (segment-endpoints s1)
   (segment-endpoints s2)
   point-equal?))

(define (point-equal? p1 p2)
  (and (close-enuf? (point-x p1) (point-x p2))
       (close-enuf? (point-y p1) (point-y p2))))
\end{code-listing}

\subsection{Numerical Accuracy}

Throughout the system, numerical accuracy issues and floating point
errors arise when comparing objects. As a result, the system uses
custom equality operators for each data type, such as
\texttt{point-equal?} shown in Listing~\ref{segment-equal-eqv}. These
use an underlying floating-point predicate \texttt{close-enuf?} taken
from the MIT Scheme Mathematics Library \cite{scmutils} that estimates
and sets a tolerance based on current machine's precision and handles
small magnitude values intelligently. With this floating point
tolerance in comparisons, floating point errors have been
significantly less prevalent.

\section{Analysis Procedure}

Given these relationship and observation structures,
Listing~\ref{analyzer} presents the main analyzer routine in this
module. After extracting various types of elements from the figure, it
examines the relationships relevant for each set of elements and
gathers all resulting observations.

\begin{code-listing}
[label=analyzer]
{Analyzer Routine}
(define (analyze figure)
  (let* ((points (figure-points figure))
         (angles (figure-angles figure))
         (linear-elements (figure-linear-elements figure))
         (segments (figure-segments figure)))
    (append
     (extract-relationships points
                            (list concurrent-points-relationship
                                  concentric-relationship
                                  concentric-with-center-relationship))
     (extract-relationships segments
                             (list equal-length-relationship))
     (extract-relationships angles
                             (list equal-angle-relationship
                                   supplementary-angles-relationship
                                   complementary-angles-relationship))
     (extract-relationships linear-elements
                             (list parallel-relationship
                                   concurrent-relationship
                                   perpendicular-relationship)))))
\end{code-listing}

The workhorses of \texttt{extract-relationships} and
\texttt{report-n-wise} shown in Listing~\ref{extract-relationships}
generate the relevant n-tuples and report observations for those that
satisfy the relationship under consideration. For these homogeneous
cases, \texttt{all-n-tuples} returns all (unordered) subsets of size
$n$ as lists.

\begin{code-listing}
[label=extract-relationships]
{Extracting Relationships}
(define (extract-relationship elements relationship)
  (map (lambda (tuple)
         (make-observation relationship tuple))
       (report-n-wise
        (relationship-arity relationship)
        (relationship-predicate relationship)
        elements)))

(define (report-n-wise n predicate elements)
  (let ((tuples (all-n-tuples n elements)))
    (filter (nary-predicate n predicate) tuples)))
\end{code-listing}

\section{Focusing on Interesting Observations}

The final aspect of the perception module involves filtering out
obvious and previously discovered observations.  Several techniques
will be discussed but Listing~\ref{interesting-obs-impl} shows the
module's current state of accomplishing this task via the
\texttt{interesting-observations} procedure. The procedure first
extracts all observations from the figure and aggregates a list of
obvious relations specified during the construction. It then uses the
learning module to examine all polygons found in the figure and
determine the most specific definitions each satisfies. The procedure
obtains all previously-discovered facts about such shapes to remove
from the final result and adds new polygon observations in their
place. Example~\ref{ortho-interesting} shows a concrete example of
this.

\newpage

\begin{code-listing}
[label=interesting-obs-impl]
{Obtaining Interesting Observations}
(define (interesting-observations figure-proc)
  (set! *obvious-observations* '())
  (let* ((fig (figure-proc))
         (all-obs (analyze-figure fig))
         (polygons (figure-polygons fig))
         (polygon-observations (polygon-observations polygons))
         (polygon-implied-observations
           (polygon-implied-observations polygons))
    (set-difference (append all-obs polygon-observations)
                    (append *obvious-observations* polygon-implied-observations)
                    observation-equivalent?)))
\end{code-listing}

Listing~\ref{mark-obvious} shows the implementation of the
\texttt{save-obvious-observation!} procedure. Construction procedures
can use this to mark obvious relationships for the elements they
create. For instance, the procedure that creates the perpendicular
bisector of a segment creates and saves an observation that the line
it is creating is perpendicular to the original segment before
returning the bisector line.

\begin{code-listing}
[label=mark-obvious]
{Marking Obvious Observations}
(define (save-obvious-observation! obs)
  (if *obvious-observations*
      (set! *obvious-observations*
            (cons obs *obvious-observations*))))
\end{code-listing}

Example~\ref{interesting-analysis} in the demonstration chapter
demonstrated a simple example of a construction procedure that marked
obvious properties of its results. Example~\ref{ortho-interesting},
demonstrates the other, polygon definition-based technique of
simplifying observations. Although there were 21 total observations
found in the resulting figure, after examining the types of polygons
in the figure and removing observations previously discovered about
those elements, only two observations remain:

\begin{img-example}
[label=ortho-interesting]
{Substituting Polygon Observations}
{images/inner-investigation-2.png}
(define (orthodiagonal-inner-polygon)
  (let-geo*
      (((oq (a b c d)) (random-orthodiagonal-quadrilateral))
       (e (midpoint a b))
       (f (midpoint b c))
       (g (midpoint c d))
       (h (midpoint d a))
       (inner-p (polygon-from-points e f g h)))
    (figure oq inner-p)))

=> (length (all-observations orthodiagonal-inner-polygon))
21

=> (pprint (interesting-observations orthodiagonal-inner-polygon)
((orthodiagonal oq) (rectangle inner-p))
\end{img-example}

\section{Discussion and Extensions}

Perfectly determining what observations are interesting or non-obvious
is a large task, particularly since filtering out obvious relations
often requires relationship-specific information:

As one example, imagine implementing a \texttt{collinear?}  predicate
that only reports non-obvious relations. Testing whether three
arbitrary coordinate-based points are collinear is
straightforward. However, it is not interesting that a random point on
a line is collinear with the two points from which the line was
defined. In order to accurately know whether or not it is interesting
that such points are collinear, the system would need to have access
to a graph-like representation of which points were specified to be on
which lines. Similar auxiliary structures can help filter other types
of relationships.

The analysis routine was initially one large, complicated procedure in
which individual checks were arbitrarily added. The restructuring to
use relationships and observations has simplified the complexity and
enabled better interactions with the learning module, but limited the
ability for adding many relationship-specific optimizations.

Despite these limitations, the perception system has been sufficient
to discover several relations via the learning model and use basic
filtering of obvious relations to present intelligible results.

The examples below describe further efforts explored for improving the
perception module. These involve extracting relationships for elements
not explicitly specified in a figure, such as auxiliary segments
between all pairs of points in the figure, treating all intersections
as points, extracting angles, or merging results. These are areas for
future work.

\subsection{Auxiliary Segments}

In some circumstances, it is useful for the system to insert and
consider segments between all pairs of points. Although this can
sometimes produce interesting results, it can often lead to too many
uninteresting observations. This option is off by default but could be
extended and enabled in a self-exploration mode, for instance.

\subsection{Extracting Angles}

In addition, I briefly explored an implementation in which the
construction module also maintains a graph-like representation of the
connectedness and adjacencies in the figure. Such a representation
could help with extracting angles not explicitly created in a
figure. However, in addition to the complexity of determining which
angles to keep, keeping track of obvious relationships between such
extracted angles due to parallel lines, for instance, is quite a
challenge.

\subsection{Merging Related Observations}

A final process I explored involved merging related observations into
larger, combined results. For instance, when reporting segment length
equality for a square, it is excessive to report all possible pairs of
equal sides. I initially implemented a step to merge such observations
to simply report that all four sides are congruent. However, as more
relationships were added, the merge process became complicated as the
arguments for all observations were not commutative and transitive.
For example merging relationships about angles being supplementary to
one another and merging sets of three concentric points with a fourth
as its center would each require a unique merge
procedure. Generalizing and adding such merge procedures would be an
interesting extension.

%% Initial Stuff:

\if false

\subsection{Extracting segments and angles}

The observation module also builds and traverses a
graph-representation of the object of connectedness and adjacencies to
extract more segments and angles, or include intersections of elements
in its investigation.

\subsection{Removing Obvious Properties}

This module makes use of available dependency information to eliminate
some obvious properties. At this phase, the eliminations arise only
from basic geometry knowledge ``hard-coded'' into the system, and not
upon any specific prior-learned formula.

\subsubsection{Trivial relations}

Points being on lines, segments, circles directly dependent on that point.

\subsubsection{Branch Relations}

Other examples include ``branch'' relations. [REF: Chen, Song,
  etc.]. ABCD on a line with AB = CD also means that AC = BD, for instance.

\section{Representations}

A ``Relationship'' object represents a type of relationship, a
``Observation'' object refers to a specific observation seen in a
figure.

\fi
