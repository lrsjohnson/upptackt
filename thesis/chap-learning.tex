\chapter{Learning Module}
\label{chap:learning}

\section{Overview}

As the final module, the learning module integrates information from
the other modules and provides the primary, top-level interface for
interacting with the system. It provides means for users to query its
knowledge and provide investigations for the system to carry
out. Through performing such investigations, the learning module
formulates conjectures based on its observations and maintains a
repository of information representing a student's understanding of
geometry concepts.

I will first discuss the interface for interacting with the
system. Then, after describing the structures for representing and
storing definitions and conjectures, I demonstrate how the system
module new terms and conjectures. Finally, I will explain the cyclic
interaction between the imperative and declarative modules used to
simplify definitions and discuss some limitations and future
extensions.

%% Outline
%% - Interface (what-is, etc.) / Student
%% - Definitions and Conjectures, Lattice
%% - Learning terms and conjectures
%% - Simplifying definitions
%% - Discussion

\section{Interface with Student}

The learning module provides the primary interface by which users
interact with the system. As such, it provides means by which users
can both query the system to discover and use what it has known, as
well as to teach the system information by suggesting investigations
it should undertake.

\begin{code-example}{Using Definitions}
(define (what-is term)
  (pprint (lookup term)))

(define (is-a? term obj)
  (let ((def (lookup term)))
    (if (unknown? def)
        `(,term unknown)
        ((definition-predicate def) obj))))

(define (show-me term)
  (let ((def (lookup term)))
    (if (unknown? def)
        `(,term unknown)
        (show-element ((definition-generator def))))))

(define (examine object)
  (show-element object)
  (let ((applicable-terms
         (filter (lambda (term)
                   (is-a? term object))
                 (all-known-terms))))
    applicable-terms))
\end{code-example}

\subsection{Querying}

A simple way of interacting with the learning module is to ask it for
what it knows about various geometry concepts or terms. For
definitions, the results provide the classification (that a rhombus is
a parallelogram), and a set of minimal properties that differentiates
that object from its classification. Further querying can present all
known properties of the named object as well as theorems involving
that term.

\section{Representing Definitions and Conjectures}

Discoveries are represented within a lattice of premises (discoveries
about quadrilaterals < discoveries about rhombuses < discoveries about
squares, but are separate from discoveries about circles or segments).


\section{Learning new Terms and Conjectures}

To learn a new definition, the system must be given the name of the
term being learned as well as a procedure that will generate arbitrary
instances of that definition. To converge to the correct definition,
that random procedure should present a wide diversity of instances
(i.e. the random-parallelogram procedure should produce all sorts of
parallelograms, not just rectangles). However, reconciling mixed
information about what constitutes a term could be an interesting
extension.

\begin{code-listing}{Learning a new term}
(define (learn-term term object-generator)
  (let ((v (lookup term)))
    (if (not (eq? v 'unknown))
        (pprint `(already-known ,term))
        (let ((example (name-polygon (object-generator))))
          (let* ((base-terms (examine example))
                 (simple-base-terms (simplify-base-terms base-terms))
                 (base-definitions (map lookup base-terms))
                 (base-conjectures (flatten (map definition-conjectures
                                                 base-definitions)))
                 (fig (figure (with-dependency '<premise> example)))
                 (observations (analyze-figure fig))
                 (conjectures (map conjecture-from-observation observations))
                 (simplified-conjectures
                  (simplify-conjectures conjectures base-conjectures)))
            (pprint conjectures)
            (let ((new-def
                   (make-restrictions-definition
                    term
                    simple-base-terms
                    simplified-conjectures
                    object-generator)))
              (add-definition! *current-student* new-def)
              'done))))))
\end{code-listing}

\subsection{Predicates from Observations}

\begin{code-listing}{Building Predicates for Definitions}
(define (build-predicate-for-definition s def)
  (let ((classifications (definition-classifications def))
        (conjectures (definition-conjectures def)))
    (let ((classification-predicate
           (lambda (obj)
             (every
              (lambda (classification)
                (or ((definition-predicate (student-lookup s classification))
                     obj)
                    (begin (if *explain*
                               (pprint `(failed-classification
                                         ,classification)))
                           #f)))
              classifications))))
      (lambda args
        (and (apply classification-predicate args)
             (every (lambda (o) (satisfies-conjecture o args))
                    conjectures))))))
\end{code-listing}

\subsection{Performing Investigations}

Investigations are similar to analyzing various figures above except
that they have the intent of the analysis results being placed in the
geometry knowledge repository. This separation also allows for
dependence information about where properties were derived from.

Given the lattice structure of definitions, an interesting question when exploring
new investigations is whether the given

\section{Simplifying Definitions}

To Simplify definitions, we interface with the constraint solver
observations->figure to convert our observations back into a figure.

\begin{code-listing}{Simplifying Definitions}
(define (get-simple-definitions term)
  (let ((def (lookup term)))
    (if (unknown? def)
        (error "Unknown term" term))
    (let* ((object ((definition-generator def)))
           (observations
            (filter
             observation->constraint
             (all-observations
              (figure (name-polygon object))))))
      (map
       (lambda (obs-subset)
         (pprint obs-subset)
         (let* ((topology (topology-for-object object))
                (new-figure
                 (observations->figure topology obs-subset)))
           (if new-figure
               (let ((new-polygon
                      (polygon-from-figure new-figure)))
                 (pprint new-polygon)
                 (if (is-a? term new-polygon)
                     (list 'valid-definition
                           obs-subset)
                     (list 'invalid-definition
                           obs-subset)))
               (list 'unknown-definition
                     obs-subset))))
       (all-subsets observations)))))
\end{code-listing}

\begin{code-listing}{Converting Observations to a Figure}
(define (observations->figure topology observations)
  (initialize-scheduler)
  (pprint (observations->constraints observations))
  (let ((m (apply
            m:mechanism
            (list
             topology
             (observations->constraints observations)))))
    (m:build-mechanism m)
    (if (not (m:solve-mechanism m))
        (begin
          (pp "Could not solve mechanism")
          #f)
        (let ((f (m:mechanism->figure m)))
          (pp "Solved!")
          (show-figure f)
          f))))
\end{code-listing}

\section{Discussion}

%% Old Stuff:

\if false


\section{Representing Discoveries}


\fi
