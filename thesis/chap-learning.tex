\chapter{Learning Module}
\label{chap:learning}

\section{Overview}

As the final module, the learning module integrates information from
the other modules and provides the primary, top-level interface for
interacting with the system. It provides means for users to query its
knowledge and provide investigations for the system to carry
out. Through performing such investigations, the learning module
formulates conjectures based on its observations and maintains a
repository of information representing a student's understanding of
geometry concepts.

I will first discuss the interface for interacting with the
system. Then, after describing the structures for representing and
storing definitions and conjectures, I demonstrate how the system
module new terms and conjectures. Finally, I will explain the cyclic
interaction between the imperative and declarative modules used to
simplify definitions and discuss some limitations and future
extensions.

The Demonstration Chapter (Sections~\ref{sec:end-goal-1}
and~\ref{sec:end-goal-2}) included several use cases and examples of
working with the learning module. As a result, this discussion will
focus on structures and implementation rather than uses and
applications. Refer to the demonstration for examples.

%% Outline
%% - Interface (what-is, etc.) / Student
%% - Definitions and Conjectures, Lattice
%% - Learning terms and conjectures
%% - Simplifying definitions
%% - Discussion

\section{Learning Module Interface}

As seen in the demonstration, the learning module provides the primary
interface by which users interact with the system. As such, it
provides means by which users can both query the system to discover
and use what it has known, as well as to teach the system information
by suggesting investigations it should
undertake. Listing~\ref{l-interface} shows the implementation for some
of these methods.

\begin{code-example}
[label=l-interface]
{Learning System Interface Examples}
(define (what-is term)
  (pprint (lookup term)))

(define (example-object term)
  ((definition-generator (lookup term))))

(define (show-example term)
    (show-element (example-object term))

(define (is-a? term obj)
  (let ((def (lookup term)))
    (definition-holds? def obj)))

(define (examine object)
  (let ((satisfying-terms
         (filter (lambda (term) (is-a? term object))
           (known-terms))))
    (remove-supplants more-specific? satisfying-terms)))
\end{code-example}

Explaining these interface implementations provide a context for
introducing the representation of definitions and conjectures.
\enlargethispage*{\baselineskip}

\section{Querying}

Users can query the system's knowledge using \texttt{what-is}. When
queried, the system uses \texttt{lookup} to find a definition from its
dictionary. Printing this definition provides the classification (that
a rhombus is a parallelogram) and a set of properties that
differentiates that object from its classification. Further requests
can present all known properties of the named object or generate a
minimal set of properties needed to specify the object.

\subsection{Student Structure}

Internally, geometry knowledge is stored in a \texttt{student} object
that maintains a \texttt{dictionary} mapping terms to definitions and
a \texttt{term lattice} representing how these definitions relate to
one another. Listing~\ref{student-structure} demonstrates how the
interfaces above use a global \texttt{*current-student*} variable to
access information. Although the system currently only ever
instantiates one student, this architecture provides the flexibility
to teach or compare multiple students in the future.

\begin{code-listing}
[label=student-structure]
{Student Structure}
(define-record-type <student>
  (%make-student definition-dictionary term-lattice) ...)

(define (student-lookup-definition s name)
  (hash-table/get (student-dictionary s) name #f))

(define *current-student* (make-initialized-student))

(define (lookup-definition term)
  (student-lookup-definition *current-student* term))

(define (lookup term)
  (or (lookup-definition term) (error "Term Unknown:" term)))
\end{code-listing}

\subsection{Definition Structure}

\begin{code-listing}
[label=def-struct]
{Definition Structure}
(define-record-type <definition>
  (%make-definition name generator primitive-predicate
                     primitive?
                     all-conjectures
                     classifications specific-conjectures) ...)
\end{code-listing}

Listing~\ref{def-struct} shows the implementation of definition
structures. Definition combine the name and generator procedure
provided when originally learning the definition with a list of all
conjectures known about that class of object. \texttt{primitive?} is a
boolean indicator of whether the definition is a primitive, built-in
definition. In such cases, \texttt{base-predicate} is a imperative
system-level predicate that tests whether an object satisfies the
definition. In non-primitive definitions, the \texttt{base-predicate}
is that of the primitive that the definition is a specialization
of. Storing and checking against this primitive predicate prevents
inapplicable operations from being performed such as attempting to
obtain the angles of a segment object.

The last two fields, \texttt{classifications} and \texttt{specific
  conjectures} are derived fields that are updated based on the
definition's relation to other terms. A definition's
\texttt{classifications} are the next-least specific terms its class
of objects also satisfy and \texttt{specific-conjectures} are added
conjectures that differentiate the definition from being the union of
those classification definitions.


\section{Testing Definitions}

The learning module provides the \texttt{is-a?} procedure to test
whether a given object satisfies a known term. As shown in
Listing~\ref{def-holds}, testing whether a definition holds involves
ensuring that it is the right type of object by checking the
underlying primitive predicate and then ensuring the relevant
conjectures are satisfied.

In this nonrecursive version, the system checks that an object
satisfies \emph{all} known conjectures. A recursive version shown
later first checks that it satisfies the parent classifications before
checking definition-specific conjectures that differentiate it from
its classifications.

\begin{code-listing}
[label=def-holds]
{Definition Checking}
(define (definition-holds-nonrecursive? def obj)
  (let ((all-conjectures (definition-conjectures def)))
    (and ((definition-primitive-predicate def) obj)
         (every (lambda (conjecture)
                  (satisfies-conjecture? conjecture (list obj)))
                all-conjectures))))
\end{code-listing}

\subsection{Conjecture Structure}

Conjectures are similar to observations in that they associate a
perception relationship with information about what satisfies the
relationship. However, instead of associating a relationship with
actual elements that satisfy the relationship, conjectures abstract
this observation by storing only the symbolic dependencies and source
procedures of those arguments.

Similar to how Example~\ref{new-premise} in the imperative system used
the element source procedures to obtain constructed elements
corresponding to those observed in an original diagram, satisfying a
conjecture involves applying its source-procedures to a new premise
structure for the conjecture to obtain new relationship
arguments. These new arguments are then checked to see if they satisfy
the underlying relationship. This process is shown in
Listing~\ref{conj-struct}. The interface procedure \texttt{is-a?}
creates a list of the object in question to use as the new premise.

\begin{code-listing}
[label=conj-struct]
{Conjecture Structure}
(define-record-type <conjecture>
  (make-conjecture dependencies source-procedures relationship) ...)

(define (satisfies-conjecture? conj premise-instance)
  (or (true? (observation-from-conjecture conj premise-instance))
      (begin (if *explain* (pprint `(failed-conjecture ,conj)))
             #f)))

(define (observation-from-conjecture conj premise-instance)
  (let ((new-args
         (map (lambda (construction-proc)
                (construction-proc premise-instance))
          (conjecture-construction-procedures conj)))
        (rel (conjecture-relationship conj)))
    (and (relationship-holds rel new-args)
         (make-observation rel new-args))))
\end{code-listing}

\section{Examining Objects}

Given these tests, \texttt{examine}, the last interface function shown
in Listing~\ref{l-interface} allows a user to provide a geometry
object and ask the system to examine it and report what it is. Its
implementation (in Listing~\ref{l-interface}) first determines all
terms that apply to an object and then removes terms that are
supplanted by others in the list. It uses the procedure
\texttt{more-specific?} to determine which terms supplant others. As
shown in Listing~\ref{more-specific}, this procedure generates checks
if an example object of the proposed less specific term satisfies the
definition of the proposed more specific term.

\begin{code-listing}
[label=more-specific]
{Relations among terms}
(define (more-specific? more-specific-term less-specific-term)
  (let ((more-specific-obj (example-object more-specific-term)))
    (is-a? less-specific-term more-specific-obj)))
\end{code-listing}


\subsection{Maintaining the Term Lattice}

In addition to helping remove redundant information in results, this
partial order on terms is used to build and maintain a lattice of
terms in the student structure. This lattice can be rendered to a
figure using dot/Graphviz as shown in Example~\ref{full-lattice-pic}.

\begin{img-example}
[label=full-lattice-pic,
breakable=false,
comment style={size=fbox,frame hidden,height=8cm}]
{Full Definition Lattice}{images/full-lattice.png}
=> (show-definition-lattice)
\end{img-example}

The lattice is implemented as a general lattice data structure I
created that can be used with any partial order comparator. It
correctly positions nodes and updates the relevant parent and child
pointers as nodes are added and removed.

Information from the lattice is used to update the derived fields of
terms. As seen in Listing~\ref{updating-terms}, after a new definition
term is added to the lattice, it and its child terms (determined from
lattice) are updated. The immediate parent nodes in the lattice become
the definitions classifications, and definition-specific conjectures
are the set difference of all their conjectures and the conjectures
known about their ancestors in the lattice.

\begin{code-listing}
[label=updating-terms]
{Updating Terms from Lattice}
(define (add-definition-lattice-node! term)
  (add-lattice-node (definition-lattice) (make-lattice-node term term))
  (update-definitions-from-lattice (cons term (child-terms term))))

(define (update-definition-from-lattice term)
  (let* ((def (lookup term))
         (current-conjectures (definition-conjectures def))
         (ancestor-terms (ancestor-terms term))
         (ancestor-defs (map lookup ancestor-terms))
         (ancestor-conjectures
          (append-map definition-conjectures ancestor-defs))
         (new-conjectures
          (set-difference current-conjectures
                          ancestor-conjectures
                          conjecture-equivalent?)))
    (set-definition-classifications! def (parent-terms term))
    (set-definition-specific-conjectures! def new-conjectures)))
\end{code-listing}

This lattice structure allows terms definitions to build off of one
another and allows definitions to report only . These updated
classification and definition-specific properties are also used in the
full version of checking when a definition holds as shown in
Listing~\ref{def-holds-2}. This version checks that a definition
satisfies all parent classifications first before checking the
definition-specific conjectures that differentiate it from those
classifications.

\begin{code-listing}
[label=def-holds-2]
{Recursive Definition Holds}
(define (definition-holds? def obj)
  (let ((classifications (definition-classifications def))
        (specific-conjectures (definition-specific-conjectures def)))
    (and ((definition-predicate def) obj)
         (every (lambda (classification-term)
                  (is-a? classification-term obj))
                classifications)
         (every (lambda (conjecture)
                  (satisfies-conjecture? conjecture (list obj)))
                specific-conjectures))))
\end{code-listing}

\subsection{Core Knowledge}

To initialize the system, the student structure is provided with
several primitive definitions at startup as shown in
Listing~\ref{core-knowledge}.

\begin{code-listing}
[label=core-knowledge]
{Introducing Core Knowledge}
(define (provide-core-knowledge)
  (for-each add-definition! primitive-definitions))

(define primitive-definitions
  (list
   (make-primitive-definition 'object true-proc true-proc)
   (make-primitive-definition 'point point? random-point)
   (make-primitive-definition 'line line? random-line)
   ...
   (make-primitive-definition 'triangle triangle? random-triangle))
\end{code-listing}

\section{Learning new Terms and Conjectures}

To learn a new definition, the system must be given the name of the
term being learned as well as a procedure that will generate arbitrary
instances of that definition. To converge to the correct definition,
that random procedure should present a wide diversity of instances
(i.e. the random-parallelogram procedure should produce all sorts of
parallelograms, not just rectangles). However, reconciling mixed
information about what constitutes a term could be an interesting
extension.

\begin{code-listing}
[label=learn-term]
{Learning a new term}
(define (learn-term term object-generator)
  (if (term-known? term) (error "Term already known:" term))
  (let ((term-example (name-polygon (object-generator))))
    (let* ((primitive-predicate (get-primitive-predicate term-example))
           (fig (figure (as-premise term-example 0)))
           (observations (analyze-figure fig))
           (conjectures (map conjecture-from-observation observations)))
      (pprint conjectures)
      (let ((new-def
             (make-definition term object-generator
                primitive-predicate conjectures)))
        (add-definition! new-def)
        (check-new-def new-def)
        'done))))

(define (conjecture-from-observation obs)
  (make-conjecture
   (map element-dependencies->list (observation-args obs))
   (map element-source (observation-args obs))
   (observation-relationship obs)))
\end{code-listing}

Listing~\ref{learn-term} shows the implementation of the
\texttt{learn-term} procedure. It uses the provided generator
procedure to produce an example object for the term, creates a figure
with that object as its premise and obtains observations. These
observations are converted to conjectures via
\texttt{conjecture-from-observation} and the resulting definition is
added to the student dictionary and term lattice.

\subsection{Performing Investigations}

As demonstrated in Example~\ref{diag-investigation} (page
\pageref{diag-investigation}), the learning module also supports
investigations to learn conjectures based on elements constructed from
the base element.  Performing investigations are similar to learning
terms except that, rather than providing a procedure that just
generates an example of the term in consideration, an investigation
uses a procedure which takes an instance of the premise (polygon in
these cases) and constructs an entire figure to analyze. In addition
to reporting the interesting observations of such investigations,
conjectures for new observations derived by that investigation are
added to the definition for the term under investigation.

\section{Simplifying Definitions}

As properties accumulate from analysis and investigation, the need to
satisfy all known properties for a shape overconstraints the resulting
definitions. Thus, the final role of the learning module is to
simplify term definitions by checking declarative constraints.

As seen in Listing~\ref{simple-def}, \texttt{get-simple-definitions}
takes a known term, looks up the known properties for that term, and
tests all reasonable subsets of those properties as constraints using
the constraint solver. For each subset of properties, if the
constraint solver was able to create a diagram satisfying exactly
those properties, the resulting diagram is checked using with the
\texttt{is-a?} procedure to see if all the other known properties of
the original term still hold.

If so, the subset of properties is reported as a sufficient definition
of the term, and if the resulting diagram fails some properties, the
subset is reported as an insufficient set of constraints. These
resulting sufficient definitions can be treated as equivalent, simpler
definitions and used as the premises in new theorems about the
objects.

\begin{code-listing}
[label=simple-def]
{Simplifying Definitions}
(define (get-simple-definitions term)
  (let ((def (lookup term))
        (simple-def-result (make-simple-definitions-result)))
    (let* ((object ((definition-generator def)))
           (fig (figure (as-premise (name-polygon object) 0)))
           (all-observations (analyze-figure fig))
           (eligible-observations
            (filter observation->constraint all-observations)))
      (for-each
       (lambda (obs-subset)
         (if (simple-def-should-test? simple-def-result obs-subset)
             (let ((polygon
                    (polygon-from-object-observations object obs-subset)))
               ((cond ((false? polygon) mark-unknown-simple-def!)
                      ((is-a? term polygon) mark-sufficient-simple-def!)
                      (else mark-insufficient-simple-def!))
                simple-def-result obs-subset)
               (simplify-definitions-result! simple-def-result))
             (pprint `(skipping ,obs-subset))))
       (shuffle (all-subsets eligible-observations)))
      simple-def-result)))
\end{code-listing}

The \texttt{simple-definitions-result} structure maintains information
about what subsets are known to sufficient or insufficient as the
analysis proceeds and provides the predicate
\texttt{simple-def-should-test?} to skip over subsets where the result
is already known.

The main workhorse in this definition simplification process is the
procedure \texttt{polygon-from-object-observations}. It interfaces
with the constraint solver via observations->figure to convert our
observations back into a figure. Its implementation is shown below in
Listing~\ref{convert-obs}. The object provided is used to determine
the topology and names of bars and linkages in the mechanism and the
objects in the observation structures in the provided observation
subset is used to add the necessary mechanism constraints. If the
declarative system can solve the mechanism, it once again uses the
element names to extract the resulting object and return the a figure.

\begin{code-listing}
[label=convert-obs]
{Converting Observations to a Figure}
(define (polygon-from-object-observations object obs-subset)
  (let* ((topology (topology-for-object object))
         (new-figure (observations->figure topology obs-subset)))
    (and new-figure (object-from-new-figure object new-figure))))

(define (establish-polygon-topology-for-polygon polygon)
  (let* ((points (polygon-points polygon))
         (vertex-names (map element-name points)))
    (apply m:establish-polygon-topology vertex-names)))

(define (observations->figure-one-trial topology observations)
  (initialize-scheduler)
  (let* ((constraints (observations->constraints observations))
         (m (m:mechanism topology constraints)))
    (m:build-mechanism m)
    (and (m:solve-mechanism m)
         (let ((fig (m:mechanism->figure m)))
           (show-figure fig)
           fig))
\end{code-listing}

\newpage
\section{Discussion}

The learning module has been able to successfully integrate with the
other system modules to discover and learn dozens of simple elementary
geometry terms and theorems through its investigations. These include
simple properties such as ``the base angles in an isoceles triangle
are congruent,'' derived properties such as ``the diagonals of a
rhombus are orthogonal and bisect one another'' or ``the polygon found
by connecting consecutive side midpoints of an orthodiagonal
quadrilateral is always a rectangle,'' and simplified definitions such
as ``a quadrilateral with two pairs of congruent opposite angles is a
parallelogram.''

The current system has focused on discoveries related to
polygons. Further extensions of the module could explore ideas related
to other object types (segments, lines, circles) or derive conjectures
that depend on several arbitrary choices. Finally, an interesting
extension of the learning module would be to investigation properties
about constructions ``this is how you create a perpendicular
bisector'' so that the system could infer what interesting properties
such constructions yield and omit those observations when that
construction is used.
