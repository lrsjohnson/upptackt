\chapter{Imperative Construction System}
\label{chap:imperative}

\section{Overview}

The first module is an imperative system for performing geometry
constructions. This is the typical input method for generating
coordinate-backed, analytic instances of figures.

The construction system is comprised of a large, versatile library of
useful utility and construction procedures for creating figures. To
appropriately focus the discussion of this module, I will concentrate
on the implementation of structures and procedures necessary for the
sample construction seen in Example~\ref{sample-construction}.  Full
code and more usage examples are provided in Appendix \ref{chap:code}.

In doing so, I will first describe the basic structures and essential
utility procedures before presenting some higher-level construction
procedures, polygons, and figures. Then, I will explore the use of
randomness in the system and examine how construction language macros
handle names, dependencies, and multiple assignment of components.
Finally, I will briefly discuss the interface and implementation for
animating and displaying figures.

\begin{pdf-example}
[label=sample-construction]
{Sample Construction Figure for Chapter}
{images/angle-bisector-distance.pdf}
(define (angle-bisector-distance)
  (let-geo* (((a (r-1 v r-2)) (random-angle))
             (ab (angle-bisector a))
             (p (random-point-on-ray ab))
             ((s-1 (p b)) (perpendicular-to r-1 p))
             ((s-2 (p c)) (perpendicular-to r-2 p)))
     (figure a r-1 r-2 ab p s-1 s-2)))

=> (show-figure (angle-bisector-distance))
\end{pdf-example}

The sample construction in Example~\ref{sample-construction}
constructs perpendiculars from an arbitrary point on an angle bisector
to the ray extensions of the angle being bisected.


%% Outline
%% - Basic Structures and essential Utilities (code + line-from-points...)
%% - Randomness
%% - Higher-order Objects (Polygons, Figure)
%% - let-geo* Names and Dependencies
%% - Animation / Graphics.

\section{Basic Structures}

The basic structures in the imperative construction system are points,
segments, rays, lines, angles, and circles. These structures, as with
all structures in the system are implemented using Scheme record
structures as seen in Listings \ref{basic-structures} and
\ref{angle-circle-structs}. In the internal representations, a segment
is two ordered endpoints, a ray is an endpoint and a direction, and a
line is a point and a direction from that point the line
extends. Thus, lines and segments are directioned, and the same
geometric line and segment can have several different internal
representations. Predicates exist to allow other procedures to work
with or ignore these distinctions.

\begin{code-listing}
[label=basic-structures]
{Basic Structures}
(define-record-type <point>
  (make-point x y)
  point?
  (x point-x)
  (y point-y))

(define-record-type <segment>
  (make-segment p1 p2)
  segment?
  (p1 segment-endpoint-1)
  (p2 segment-endpoint-2))

(define-record-type <line>
  (make-line point dir)
  line?
  (point line-point) ;; Point on the line
  (dir line-direction))
\end{code-listing}

As shown in Listing~\ref{angle-circle-structs}, angles are represented
as a vertex point and two arm directions, and circles have a center
point and radius length.

\begin{code-listing}
[label=angle-circle-structs]
{Angle and Circle Structures}
(define-record-type <angle>
  (make-angle dir1 vertex dir2)
  angle?
  (dir1 angle-arm-1)
  (vertex angle-vertex)
  (dir2 angle-arm-2))

(define-record-type <circle>
  (make-circle center radius)
  circle?
  (center circle-center)
  (radius circle-radius))
\end{code-listing}

\subsection{Creating Elements}

Elements can be created explicitly using the underlying
\texttt{make-*} constructors defined with the record types. However,
several higher-order constructors are provided to simplify
construction as shown in Listings~\ref{line-from-points}
and~\ref{angle-from}.  In \texttt{angle-from-lines}, we make use of
the fact that lines are directioned to uniquely specify an angle.

\begin{code-listing}
[label=line-from-points]
{Higher-order Constructors}
(define (line-from-points p1 p2)
  (make-line p1 (direction-from-points p1 p2)))

\end{code-listing}

\begin{code-listing}
[label=angle-from]
{Generic Constructors for Creating Angles}
(define angle-from (make-generic-operation 2 'angle-from))

(define (angle-from-lines l1 l2)
  (let ((d1 (line->direction l1))
        (d2 (line->direction l2))
        (p (intersect-lines l1 l2)))
    (make-angle d1 p d2)))
(defhandler angle-from angle-from-lines line? line?)
\end{code-listing}

Listing~\ref{angle-from} also demonstrates one of many places in the
system where I use generic operations to add flexibility. Here,
\texttt{angle-from-lines} is defined as the handler for the generic
operation \texttt{angle-from} when both arguments are lines. Similar
handlers exist for other combinations of linear elements.

\subsection{Essential Math Utilities}

Several math utility structures support these constructors and other
geometry procedures. One particularly useful abstraction is a
\texttt{direction} that fixes a direction in the interval $[0, 2
  \pi]$. Listing~\ref{directions} demonstrates some utilities using
directions. Similar abstractions exist for working with vectors.

\begin{code-listing}
[label=directions]
{Directions}
(define (subtract-directions d2 d1)
  (if (direction-equal? d1 d2)
      0
      (fix-angle-0-2pi (- (direction-theta d2)
                          (direction-theta d1)))))

(define (direction-perpendicular? d1 d2)
  (let ((difference (subtract-directions d1 d2)))
    (or (close-enuf? difference (/ pi 2))
        (close-enuf? difference (* 3 (/ pi 2))))))
\end{code-listing}

\section{Higher-order Procedures and Structures}

Higher-order construction procedures and structures are built upon
these basic elements and utilities. Listing~\ref{perpendiculars} shows
the implementation of the perpendicular constructions used in the
chapter's sample construction.

\begin{code-listing}
[label=perpendiculars]
{Perpendicular Constructions}
;; Constructs line through point perpendicular to linear-element
(define (perpendicular linear-element point)
  (let* ((direction (->direction linear-element))
         (rotated-direction (rotate-direction-90 direction)))
    (make-line point rotated-direction)))

;;; Constructs perpendicular segment from point to linear-element
(define (perpendicular-to linear-element point)
  (let ((pl (perpendicular linear-element point)))
    (let ((i (intersect-linear-elements pl (->line linear-element))))
      (make-segment point i))))
\end{code-listing}

Traditional constructions generally avoid using rulers and
protractors. However, as shown in Listing~\ref{angle-bisector}, the
internal implementation of the \texttt{angle-bisector} procedure uses
measurements to simplify construction instead of repeatedly
intersecting circle arcs to emulate compass sweeps. Although the
internal implementations of some constructions use measured values,
when providing the system with investigations, a user can still limit
the construction steps used to ones that could be performed using only
a compass and straight edge since the internal implementations of the
constructions operations remain opaque to the learning module.

\begin{code-listing}
[label=angle-bisector]
{Angle Bisector Construction}
(define (angle-bisector a)
  (let* ((d2 (angle-arm-2 a))
         (vertex (angle-vertex a))
         (radians (angle-measure a))
         (half-angle (/ radians 2))
         (new-direction (add-to-direction d2 half-angle)))
    (make-ray vertex new-direction)))
\end{code-listing}

\subsection{Polygons and Figures}

Polygon record structures contain an ordered list of points in
counter-clockwise order, and provide procedures such as
\texttt{polygon-point-ref} or \texttt{polygon-segment} to obtain
particular points, segments, and angles specified by indices.

Figures are simple groupings of geometry elements and provide
procedures for extracting all points, segments, angles,
and lines contained in the figure, including ones extracted from within
polygons or subfigures.

\section{Random Choices}

Given these underlying objects and operations, to allow figures to
represent general spaces of diagrams, random choices needed when
instantiating diagrams. The chapter's sample construction uses
\texttt{random-angle} and \texttt{random-point-on-ray},
implementations of which are shown in
listing~\ref{randomness}. Underlying these procedures are calls to
Scheme's random function over a specified range ($[0, 2\pi]$ for
\texttt{random-angle-measure}, for instance). Since infinite ranges
are not well supported and to ensure that the figures stay reasonably
legible for a human viewer, in \texttt{random-point-on-ray}, the
procedure \texttt{extend-ray-to-max-segment} clips a ray at the
current working canvas so a point on the ray can be selected within
the working canvas.

\enlargethispage*{0.5\baselineskip}

\begin{code-listing}
[label=randomness]
{Random Constructors}
(define (random-angle)
  (let* ((v (random-point))
         (d1 (random-direction))
         (d2 (add-to-direction d1 (rand-angle-measure))))
    (make-angle d1 v d2)))

(define (random-point-on-ray r)
  (random-point-on-segment
   (extend-ray-to-max-segment r)))

(define (random-point-on-segment seg)
  (let* ((p1 (segment-endpoint-1 seg))
         (p2 (segment-endpoint-2 seg))
         (t (safe-rand-range 0 1.0))
         (v (sub-points p2 p1)))
    (add-to-point p1 (scale-vec v t))))
\end{code-listing}
Other random elements are created by combining these random choices,
such as the random parallelogram in Listing
\ref{random-parallelogram}.  In \texttt{random-parallelogram}, a
parallelogram is constructed by constructing two rays with an random
angle between them, and selecting an arbitrary point on each. The
final point is computed using vector arithmetic to ``complete the
parallelogram''.

\begin{code-listing}
[label=random-parallelogram]
{Random Parallelogram}
(define (random-parallelogram)
  (let* ((r1 (random-ray))
         (p1 (ray-endpoint r1))
         (r2 (rotate-about (ray-endpoint r1) (rand-angle-measure) r1))
         (p2 (random-point-on-ray r1))
         (p4 (random-point-on-ray r2))
         (p3 (add-to-point p2 (sub-points p4 p1))))
    (polygon-from-points p1 p2 p3 p4)))
\end{code-listing}

\subsection{Backtracking}

The module currently only provides limited support for avoiding
degenerate cases, or cases where randomly selected points happen to be
very nearly on top of existing points. Several random choices use
\texttt{safe-rand-range} seen Listing~\ref{safe-randomness} to avoid
the edge cases of ranges, and some retry their local choices if the
object they are returning has points too close to one
another. However, further extensions could improve this system to
periodically check for unintended relationships amongst all elements
created previously in the figure and backtrack to select other values.

\begin{code-listing}
[label=safe-randomness]
{Safe Randomness}
(define (safe-rand-range min-v max-v)
  (let ((interval-size (max 0 (- max-v min-v))))
    (rand-range
     (+ min-v (* 0.1 interval-size))
     (+ min-v (* 0.9 interval-size)))))
\end{code-listing}

\section{Construction Language Support}

To simplify specification of figures, the module provides the
\texttt{let-geo*} macro which allows for a multiple-assignment-like
extraction of components from elements and automatically tags
resulting elements with their variable names and dependencies. These
dependencies are both symbolic for display and procedural so the
system can generalize observations into conjectures that can be
applied in other situations.

\subsection{Multiple Component Assignment}

Listing~\ref{letgeo-expansion} shows the multiple component assignment
expansion of a simple usage of \texttt{let-geo*}. In this case,
\texttt{((a (r-1 v r-2)) (random-angle))} will assign to the variable
\texttt{a} the resulting random angle, and to the variables
\texttt{r-1}, \texttt{v}, and \texttt{r-2} the resulting angle's
ray-1, vertex, and ray-2, respectively. If the specification were for
a random quadrilateral, such as \texttt{((s (a b c d))
  (random-square))}, the macro would assign to the variable \texttt{s}
the resulting random square, and to the variables \texttt{a},
\texttt{b}, \texttt{c} and \texttt{d} the resulting square's vertices.

\begin{repl-example}
[label=letgeo-expansion]
{Expansion of let-geo* macro}
(let-geo* (((a (r-1 v r-2)) (random-angle)))
  (figure a r-1 r-2 ...))

=> macro expands to:
(let* ((a (random-angle))
       (r-1 (element-component a 0))
       (v   (element-component a 1))
       (r-2 (element-component a 2)))
  (figure a r-1 r-2 ...))
\end{repl-example}

To handle these varied cases, the macro expands to use the generic operation
\texttt{element-component} to determine what components are extracted
from an object during multiple component assignment. As shown in
Listing~\ref{element-component}, for polygons, the components are the
point references directly, whereas angles and segments construct their
handlers from a given list of getters.

\begin{code-listing}
[label=element-component]
{Generic Element Component Handlers}
(declare-element-component-handler polygon-point-ref polygon?)

(declare-element-component-handler
 (component-procedure-from-getters
   ray-from-arm-1 angle-vertex ray-from-arm-2)
 angle?)

(declare-element-component-handler
 (component-procedure-from-getters
   segment-endpoint-1 segment-endpoint-2)
 segment?)

(define (component-procedure-from-getters . getters)
  (let ((num-getters (length getters)))
    (lambda (el i)
      (if (not (<= 0 i (- num-getters 1)))
          (error "Index out of range for component procedure: " i))
      ((list-ref getters i) el))))
\end{code-listing}

Listing~\ref{component-assignment-impl} demonstrates the multiple
assignment portion of the \texttt{let-geo*} macro in which the user's
specifications are expanded into the element-component expressions.

\begin{code-listing}
[label=component-assignment-impl]
{Multiple and Component Assignment Implementation}
(define (expand-compound-assignment lhs rhs)
  (if (not (= 2 (length lhs)))
      (error "Malformed compound assignment LHS (needs 2 elements): " lhs))
  (let ((key-name (car lhs))
        (component-names (cadr lhs)))
    (if (not (list? component-names))
        (error "Component names must be a list:" component-names))
    (let ((main-assignment (list key-name rhs))
          (component-assignments
           (make-component-assignments key-name component-names)))
      (cons main-assignment
            component-assignments))))

(define (make-component-assignments key-name component-names)
  (map (lambda (name i)
         (list name `(element-component ,key-name ,i)))
       component-names
       (iota (length component-names))))
\end{code-listing}

\subsection{Names and Dependencies}

The other task the \texttt{let-geo*} macro handles is assigning names
and dependencies to objects. As shown in Listing~\ref{element-name},
these properties are attached to elements using the
\texttt{eq-properties} method.  In this approach, an hash table is
used to store mappings of elements to property values. Similar
interfaces are provided for element dependencies and element sources.

\begin{code-listing}
[label=element-name]
{Element Names}
(define (element-name element)
  (or (eq-get element 'name)
      *unnamed*))

(define (set-element-name! element name)
  (eq-put! element 'name name))
\end{code-listing}

When an assignment is made in the \texttt{let-geo*} macro, three
pieces of information are associated with the assigned object: (1) its
\texttt{name}, taken from the variable used for the object in the let
statement, (2) its symbolic \texttt{dependency} that stores the procedure name
and arguments used to obtain the object primarily for display
purposes, and (3) a \texttt{source} procedure that allows the object to be
recreated from a different starting
premise. Example~\ref{dependency-expansion} shows the expansion of
these dependencies in a very simple construction. These dependencies
are added after the multiple component assignments are expanded so
will apply to all variables created in the form.

\begin{repl-example}
[label=dependency-expansion]
{Dependency and Name Assignment}
(let-geo*
    ((s (make-segment a b)))
  (figure s))

=> macro expands to:
(let* ((s (make-segment a b)))
  (set-element-name! s 's)
  (set-source! s
   (lambda (p)
     (make-segment (from-new-premise p a) (from-new-premise p b))))
  (set-dependency-if-unknown! s (list 'make-segment a b))
  (figure s))
\end{repl-example}

The decision to attach a procedure of a premise argument to an element
as its \texttt{source} allows other starting premises to be injected
during later explorations in the learning
module. \texttt{from-new-premise} allows the system to recreate the
corresponding object for a specified element given a different
premise. Example~\ref{new-premises} shows the implementation of
\texttt{from-new-premise} and the interface for specifying an explicit
premise dependency via \texttt{set-as-premise!}.  To allow for
multiple premises to be injected, the premise structure is represented
as a list.

\begin{code-example}
[label=new-premises]
{Using sources with new premises}
(define (from-new-premise new-premise element)
  ((element-source element) new-premise))

(define (set-as-premise! element i)
  (set-dependency! element (symbol '<premise- i '>))
  (set-source! element (lambda (p) (list-ref p i))))
\end{code-example}

These source and premise structures will be used more later in
learning new terms, but Example~\ref{new-premise} provides a concrete
example of its use. The first definition creates a random square and
obtains the intersection point of its two diagonals. \texttt{let-geo*}
sets up the names and dependencies, and the square is marked as the
initial premise. However, the intersection point is returned rather
than a figure.  The print statements (continued on the next page) show
that while \texttt{diag-intersection-point} is a point structure with
explicit coordinates it can produce information about how it was
created.

\begin{img-example}
[label=new-premise, breakable]
{Using from-new-premise}
{images/new-premise.png}
(define diag-intersection-point
  (let-geo*
      (((sq (a b c d)) (random-square))
       (diag-1 (make-segment a c))
       (diag-2 (make-segment b d))
       (p (intersect-linear-elements diag-1 diag-2)))
    (set-as-premise! sq 0)
    p))

=> (pp diag-intersection-point)
#[point 26] (x -.1071) (y -0.4464)

=> (print-dependencies (element-dependency diag-intersection-point))
(intersect-linear-elements
 (make-segment (element-component <premise-0> 0)
               (element-component <premise-0> 2))
 (make-segment (element-component <premise-0> 1)
               (element-component <premise-0> 3)))

(define new-figure
  (let-geo* (((k (e f g h)) (random-kite))
             (p2 (from-new-premise (list k) diag-intersection-point)))
    (figure k p2)))

=> (show-figure new-figure)
\end{img-example}

The second definition at the end of Example~\ref{new-premise},
\texttt{new-figure}, demonstrates using \texttt{from-new-premise} to
apply source information from an existing object to a new premise.
The specification of \texttt{new-figure} constructs a random kite and
uses that object \texttt{k} as the new premise in creating point
\texttt{p2} using the source information of the
\texttt{diag-intersection-point}. As seen in the image,
\texttt{from-new-premise} was able to correctly extract the
construction steps about how \texttt{diag-intersection-point} was
constructed and apply it to the new kite to set \texttt{p2} at the
intersection of the kite's diagonals.

A similar process to this example will appear in an abstracted form
later in the learning module as the system tests whether conjectures
apply to new situations.

\newpage
Listing~\ref{dep-impl} presents the implementation of the
manipulations used to add dependency information to figures, and
Listing~\ref{let-geo} presents the top-level definition for the
\texttt{let-geo*} form.

\begin{code-listing}
[label=dep-impl]
{Implementation of Dependency Expressions}
(define (args-from-premise args)
  (map (lambda (arg)
         `(from-new-premise p ,arg))
       args))

(define (set-dependency-expressions assignments)
  (append-map
   (lambda (a)
     (let ((name (car a))
           (value (cadr a)))
       (if (list? value)
           (let ((proc (car value))
                 (args (cdr value)))
             `((set-source!
                ,name (lambda (p) (,proc ,@(args-from-premise args))))
               (set-dependency!
                ,name (list (quote ,proc) ,@args))))
           `((set-source! ,name (element-source ,value))
             (set-dependency! ,name (element-dependency ,value))))))
   assignments))
\end{code-listing}

\begin{code-listing}
[label=let-geo]
{Full let-geo* Implementation}
(define-syntax let-geo*
  (sc-macro-transformer
   (lambda (exp env)
     (let ((assignments (cadr exp))
           (body (cddr exp)))
       (let ((new-assignments (expand-assignments assignments))
             (variable-names (variables-from-assignments assignments)))
         (let ((result `(let*
                            ,new-assignments
                          ,@(set-name-expressions variable-names)
                          ,@(set-dependency-expressions new-assignments)
                          ,@body)))
           ;; (pp result) ;; To debug macro expansion
           (close-syntax result env)))))))
\end{code-listing}

\newpage
\section{Graphics and Animation}

Given the primitive objects and a language for specifying
constructions, the final task of the imperative system is to display
and animate figures. To do so, the system integrates with Scheme's graphics
system for the X Window System. It can include labels and highlight specific
elements, as well as display animations representing the ``wiggling''
of the diagram. Implementations of core procedures of these components
are shown in Listings~\ref{draw-figure} and~\ref{animation}.

\begin{code-listing}
[label=draw-figure]
{Drawing Figures}
(define (draw-figure figure canvas)
  (set-coordinates-for-figure figure canvas)
  (clear-canvas canvas)
  (for-each
   (lambda (element)
     (canvas-set-color canvas (element-color element))
     ((draw-element element) canvas))
   (all-figure-elements figure))
  (for-each
   (lambda (element)
     ((draw-label element) canvas))
   (all-figure-elements figure))
  (graphics-flush (canvas-g canvas)))
\end{code-listing}

To support animation, constructions can call \texttt{animate} with a
procedure \texttt{f} that takes an argument in $[0, 1]$. When the animation is
run, the system will use fluid variables to iteratively wiggle each
successive random choice through its range of
$[0,1]$. \texttt{animate-range} provides an example where a user can
specify a range to wiggle over.

\enlargethispage*{\baselineskip}

\begin{code-listing}
[label=animation]
{Animation}
(define (animate f)
  (let ((my-index *next-animation-index*))
    (set! *next-animation-index* (+ *next-animation-index* 1))
    (f (cond ((< *animating-index* my-index) 0)
             ((= *animating-index* my-index) *animation-value*)
             ((> *animating-index* my-index) 1)))))

(define (animate-range min max)
  (animate (lambda (v) (+ min (* v (- max min))))))
\end{code-listing}

\section{Discussion}

In creating the imperative construction module, the main challenges
involved settling on appropriate representations for geometry objects
and properly yet effortlessly tracking dependencies. Initial efforts
used over-specified representations such as an angle consisting of
three points and a line consisting of two points. Reducing these to
nearly-minimal representations using directions helped simplify other
operations. In addition, the module initially had each individual
construction procedure set the dependencies of the elements it
produced. Automating this in the \texttt{let-geo*} macro helped
simplify the annotation process and make the setting of source
procedures feasible.

Future extensions could provide additional construction procedures,
particularly with added support for circle and arc-related operations,
or improve the resilience of random choices. However, I believe the
imperative module provides a sufficiently versatile library of
components and procedures to enable users to specify interesting
investigations. With the ability to construct and represent figures,
the following chapters explain details of how the system is able to
make and generalize observations to learn from user-specified
constructions.


%% Old Stuff:

\if false

\subsection{Points}

Points form the basis of most elements. Throughout the system, points
are labeled and used to identify other elements.

\subsection{Linear Elements}

The linear elements of Segments, Lines, and Rays are built upon
points. Initially the internal representation of lines were that of
two points, but to simplify manipulations,

To better specify angles (see below), all linear elements, including
segments and lines are directioned. Thus, a line pointing. Predicates
exist that compare lines for equality ignoring

\subsection{Angles}

Initially angles were represented as three points, now vertex + two
directions. CCW orientation. Methods exist to determine them from
various pairs of linear elements, uses directionality of linear
elements to determine which ``quadrant'' of the angle is desired.

Given a figure, methods exist to extract angles from the diagrams in
analysis rather than specifying each angle of interest while creating
the diagram.

\section{Higher-level structures}

In addition to the basic geometry structures, the system uses several
grouping structures to combine and abstract the basic figure elements
into higher-level figures elements.

For closure of combinators, all these higher level objects are also
``Diagram objects''.

\subsection{Polygons}

Polygons are represented as groups of points.

\subsection{Figures}

Figures are currently groups of elements. In the creation of figures
we extract additional information and build a graph out of adjacent
components for use in the analysis stages.

\section{Construction Operations}

\subsection{Traditional constructions}

\begin{code-listing}{Perpendiculars}
(define (perpendicular linear-element point)
  (let* ((direction (->direction linear-element))
         (rotated-direction (rotate-direction-90 direction)))
    (make-line point rotated-direction)))

;;; endpoint-1 is point, endpoint-2 is on linear-element
(define (perpendicular-to linear-element point)
  (let ((pl (perpendicular linear-element point)))
    (let ((i (intersect-linear-elements pl (->line linear-element))))
      (make-segment point i))))

(define (angle-bisector a)
  (let* ((d1 (angle-arm-1 a))
         (d2 (angle-arm-2 a))
         (vertex (angle-vertex a))
         (radians (angle-measure a))
         (half-angle (/ radians 2))
         (new-direction (add-to-direction d2 half-angle)))
    (make-ray vertex new-direction)))
\end{code-listing}

Midpoint, perpendicular line, bisectors

\subsection{Intersections}

Generic intersections, mathematically based at line/line or
line/circle at the core. Other intersections also add the check that
the resulting point(s) are on the elements.

\subsection{Measurement-based operations}

A ``Ruler + Protractor'' is generally not permitted in traditional
construction problems. However, sometimes its nice to be able to use
measurements to more quickly compute a result (e.g.\ angle bisector by
halving angle) vs.\ going through the whole ray/circle based
construction process.

\subsection{Transformations}

Currently, rotate about a point or translate by a vector. Also
interfaces for by *random* point or vector.

\section{Randomness}

\subsection{Random Choices}

At the basis of all random

\subsection{Remembering choices}

\subsection{Backtracking}

Currently, the system does not backtrack based on random
choices. However, there are plans to perform checks on
randomly-generated elements that are too close to one another and to
retry the random choice to avoid degenerate choices.

\subsection{Avoiding almost-degenerate points}

As discussed above, randomly making choices in

\subsection{Animating choices}

I animate over a small range within the specified random
range. Top-level infrastructure determinies frames, sleeping, etc.
Constructions can request to animate functions of one arg [0, 1]. As
the figure and animation is run, each call to randomize gets a call to
random whenever their value is non-false.

\section{Dependencies}

\subsection{Implementation}

Eq-properties, etc.

\subsection{Naming}

Sometimes derived if unknown, figure out how name metadata relates to
the dependencies.

\subsection{Forcing higher-level random dependenceis}

``Inverts'' the dependency tree that would otherwise usually go
down to points. Or set-dependency! as random-square. When given an
element by the teacher, generally we don't know how the construction
was performed.

\subsection{Dependency-less diagrams}

In some cases, the dependency structure of a figure can be wiped.

\section{Construction Language}

Constructions and instruction-based investigations are specified by
scheme procedures that return the desired figures.

\subsection{Macros}

I created a let-geo* special form that is similar to Scheme's (let
\ldots) form, but sets the element names as specified so they can be more
easily referred to later.

\subsection{Multiple Assignment}

In let-geo*, I also permit some constructions to optionally map to
multiple assignments of names, such as the case in which you create a
triangle and simulatneously want to store and name the triangle's
vertex points.

\section{Graphics}

The system integrates with Scheme's graphics system for the X Window
System to display the figures for the users. The graphical viewer can
include labels and highlight specific elements, as well as display
animations representing the ``wiggling'' of the diagram.

\begin{code-listing}{Drawing}
(define (draw-figure figure canvas)
  (set-coordinates-for-figure figure canvas)
  (clear-canvas canvas)
  (for-each
   (lambda (element)
     (canvas-set-color canvas (element-color element))
     ((draw-element element) canvas))
   (all-figure-elements figure))
  (for-each
   (lambda (element)
     (canvas-set-color canvas (element-color element))
     ((draw-label element) canvas))
   (all-figure-elements figure))
  (graphics-flush (canvas-g canvas)))
\end{code-listing}

\fi
